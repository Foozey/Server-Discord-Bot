import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
	id 'org.jetbrains.kotlin.plugin.serialization' version "${kotlin_version}"
	id 'com.gradleup.shadow' version "${shadow_version}"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories here
}

dependencies {
	// Minecraft
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	// Fabric
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

	// Kotlin
	implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${project.serialization_version}"
	implementation "org.jetbrains.kotlinx:kotlinx-datetime:${project.datetime_version}"

	// Kord
	implementation shadow("dev.kord:kord-core:${project.kord_version}")
}

shadowJar {
	archiveClassifier.set("shadow")
	configurations = [project.configurations.shadow]
	relocate("dev.kord", "shadow.dev.kord")
	relocate("kotlinx.coroutines", "shadow.kotlinx.coroutines")
	relocate("kotlinx.serialization", "shadow.kotlinx.serialization")
	relocate("kotlinx.datetime", "shadow.kotlinx.datetime")
}

remapJar {
    dependsOn("shadowJar")
	inputFile.set(shadowJar.archiveFile)
}

tasks.named("remapJar") {
    doLast {
        shadowJar.archiveFile.get().asFile.delete()
    }
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(KotlinCompile).configureEach {
	kotlinOptions {
		jvmTarget = 21
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// See https://docs.gradle.org/current/userguide/publishing_maven.html
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {
		// Add repositories to publish to here
	}
}